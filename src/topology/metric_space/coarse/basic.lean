/-
Copyright (c) 2022 . All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©mi Bottinelli
-/
import topology.metric_space.basic
import topology.metric_space.isometry
/-!
# Basic definitions of coarse geometry on metric space

This file defines basic â€œcoarse metricâ€ notions on pseudo-metric spaces.
If `Î±` is a pseudo-metric space, `s t : set Î±` and `Îµ Î´ : â„`:

* `s` is `Îµ`-dense in `t` if any point of `t` is at distance at most `Îµ` from some point of `s`;
* `s` is `Î´`-separated if any two distinct points of `s` have distance greater than `Î´`.

If `f g : Î¹ â†’ Î±` and `K : â„`:

* `f` and `g` are `K`-close if given any `p : Î¹`, the distance between `f p` and `g p` is at most
  `K`.

## Main result

* `exists_coarsely_separated_coarsely_dense_with_in`:
  Given a subset `S` of the pseudo-metric space `Î±` and some non-negative `Î´`,
  there exists a set `s âŠ† S` that is both `Î´`-dense in `S` and `Î´`-separated.

## References

* [C. DruÈ›u and M. Kapovich **Geometric group theory**][MR3753580]

## Tags

coarse geometry, metric space
-/

universes u v w

open classical function set fintype function
open_locale nnreal ennreal

noncomputable theory


variables {Î± : Type u} [pseudo_metric_space Î±]
          {Î² : Type v} [pseudo_metric_space Î²]
          {Î¹ : Type w}


@[class] structure finite_balls :=
(fintype_ball : âˆ€ x : Î±, âˆ€ r : â„•, fintype (metric.ball x r))


attribute [instance] finite_balls.fintype_ball
local attribute [instance] prop_decidable

def uniformly_finite_balls_with [@finite_balls Î± _] (k : â„• â†’ â„•) :=
âˆ€ x : Î±, âˆ€ r : â„•,  card (metric.ball x r) â‰¤ k r

namespace uniformly_finite_balls_with

lemma weaken  [@finite_balls Î± _] {k k' : â„• â†’ â„•} (hk : âˆ€ r, k r â‰¤ k' r) :
  @uniformly_finite_balls_with Î± _ _ k â†’ @uniformly_finite_balls_with Î± _ _ k' :=
Î» ufbw x r, (ufbw x r).trans (hk r)


/--
If `k: â„• â†’ â„•` bounds the cardinalities of balls in `Î±`, then so does `monofy k`.
-/
noncomputable def monofy (k : â„• â†’ â„•) : â„• â†’ â„• := Î» (r : â„•), Inf $ k '' {s : â„• | r â‰¤ s}

lemma monofy_good (k : â„• â†’ â„•) : monotone (monofy k) âˆ§ (monofy k) â‰¤ k :=
begin
  split,
  { rintros x y xley,
    have : {s : â„• | y â‰¤ s} âŠ† {s : â„• | x â‰¤ s}, from Î» s yles, xley.trans yles,
    have : (k '' {s : â„• | y â‰¤ s}) âŠ† (k '' {s : â„• | x â‰¤ s}), from image_subset k this,
    unfold monofy,
    exact cInf_le_cInf
      (by {unfold bdd_below,unfold lower_bounds,use 0,simp})
      (by {use [k y,y],simp})
      this,},
  { intro x,
    unfold monofy,
    have : k x âˆˆ k '' {s : â„• | x â‰¤ s}, by {use x, simp},
    exact cInf_le
      (by {unfold bdd_below,unfold lower_bounds,use 0,simp})
      this,},
end

lemma uniformly_finite_balls_with_monofy [@finite_balls Î± _] (k : â„• â†’ â„•) :
  @uniformly_finite_balls_with Î± _ _ k â†’ @uniformly_finite_balls_with Î± _ _ (monofy k) :=
Î» ufbw x r, begin
  have : âˆƒ (s : â„•), r â‰¤ s âˆ§ k s = (monofy k) r,
  { let k_img := k '' {s : â„• | r â‰¤ s},
    let i := Inf k_img,
    unfold monofy,
    have : i âˆˆ k_img, from nat.Inf_mem (by {use k r, use r, simp,}),
    rcases this with âŸ¨s,r_le_s,ks_eq_iâŸ©,
    use [s,r_le_s],
    rw ks_eq_i,},
  rcases this with âŸ¨s,r_le_s,s_okâŸ©,
  have : metric.ball x r âŠ† metric.ball x s,
    from metric.ball_subset_ball (nat.cast_le.mpr r_le_s),
  rw â†s_ok,
  exact (card_le_of_subset this).trans (ufbw x s),
end

lemma of_transitive
  [inhabited Î±]
  [fb : @finite_balls Î± _]
  (tr : âˆ€ x y : Î±, âˆƒ f : Î± â‰ƒáµ¢ Î±, (f x = y)) :
  âˆƒ k : â„• â†’ â„•, @uniformly_finite_balls_with Î± _ _ k :=
begin
  let x := arbitrary Î±,
  use Î» (r : â„•), card (metric.ball x r),
  intros y r,
  rcases tr x y with âŸ¨fáµ¢,fxyâŸ©,
  rw â†fxy,
  have imgisball := isometric.image_ball fáµ¢ x r,
  have imgcard   := card_image_of_injective (metric.ball x r) (isometric.injective fáµ¢),
  simp_rw [imgisball,imgcard] at *,
end



end uniformly_finite_balls_with

/--
Given a pseudo-metric space `Î±`, the subset `s` is `Îµ`-dense in the subset `t`
if any point of `t` is at distance at most `Îµ` from some point of `s`.
-/
def coarsely_dense_with_in (Îµ : â„) (s t : set Î±) :=
âˆ€ â¦ƒxâ¦„ (hx : x âˆˆ t), âˆƒ â¦ƒyâ¦„ (hy : y âˆˆ s), dist x y â‰¤ Îµ

/--
Given a pseudo-metric space `Î±`, the subset `s` is `Î´`-separated
if any pair of distinct points of `s` has distance greater than `Î´`.
-/
def coarsely_separated_with  (Î´ : â„) (s : set Î±)  :=
âˆ€ â¦ƒxâ¦„ (hx : x âˆˆ s) â¦ƒyâ¦„ (hy : y âˆˆ s), x â‰  y â†’ dist x y > Î´

/--
Two maps `f g` from `Î¹` to a pseudo-metric space `Î±` are `K`-close if
for all `x : Î¹`, the distance between `f x` and `g x` is at most `K`.
-/
def close_maps_with (K : â„) (f g : Î¹ â†’ Î±) :=
âˆ€ x : Î¹ , dist (f x) (g x) â‰¤ K


namespace coarsely_dense_with_in

/--
A set is always `0`-dense in itself.
-/
lemma refl (s : set Î±) : coarsely_dense_with_in 0 s s :=
Î» x xs, âŸ¨x, xs, by simpâŸ©

/--
If `r` is `Îµ`-dense in `s`, and `s` is `Îµ'`-dense in `t`,
then `r` is `(Îµ+Îµ')`-dense in `t`.
-/
lemma trans {Îµ Îµ' : â„} {r s t : set Î±}
  (r_in_s : coarsely_dense_with_in Îµ r s) (s_in_t : coarsely_dense_with_in Îµ' s t) :
  coarsely_dense_with_in (Îµ + Îµ') r t :=
begin
  rintros z z_in_t,
  rcases s_in_t z_in_t with âŸ¨y,y_in_s,ydâŸ©,
  rcases r_in_s y_in_s with âŸ¨x,x_in_r,xdâŸ©,
  use [x, x_in_r],
  calc dist z x â‰¤ (dist z y) + (dist y x)    : dist_triangle z y x
             ... â‰¤ Îµ'          + (dist y x)  : add_le_add yd (le_refl $ dist y x)
             ... â‰¤ Îµ'          + Îµ           : add_le_add (le_refl Îµ') xd
             ... = Îµ + Îµ'                    : by ring
end

/--
If `s` is `Îµ`-dense in `t`, `s âŠ† s'`, `t' âŠ† t`, and `Îµ â‰¤ Îµ'`,
then `s'` is `Îµ'`-dense in `t'`.
-/
lemma weaken {Îµ Îµ' : â„} {s s' t t' : set Î± }
  (s_in_t : coarsely_dense_with_in Îµ s t)
  (s_sub_s' : s âŠ† s') (t'_sub_t : t' âŠ† t) (Îµ_le_Îµ' : Îµ â‰¤ Îµ') :
  coarsely_dense_with_in Îµ' s' t' :=
begin
  rintros z z_in_t',
  have z_in_t : z âˆˆ t, from t'_sub_t z_in_t',
  rcases s_in_t z_in_t with âŸ¨x,x_in_s,xdâŸ©,
  have x_in_s' : x âˆˆ s', from s_sub_s' x_in_s,
  use [x,x_in_s'],
  calc dist z x â‰¤ Îµ  : xd
            ... â‰¤ Îµ' : Îµ_le_Îµ',
end

/--
If `s` is a maximal `Î´`-separated subset of `S`, then it is `Î´`-dense in `S`.
-/
theorem of_max_coarsely_separated_with_in {Î´ : â„â‰¥0} {s S: set Î±}
  (H : s âŠ† S
     âˆ§ coarsely_separated_with Î´ s
     âˆ§ (âˆ€ t : set Î±, s âŠ† t â†’ t âŠ† S â†’  coarsely_separated_with Î´ t â†’ s = t)) :
  coarsely_dense_with_in Î´ s S :=
begin
  rcases H with âŸ¨s_sub_S, s_sep, s_maxâŸ©,
  rintros x xS,
  let t := s.insert x,
  by_contradiction H,
  push_neg at H,
  have x_notin_s : x âˆ‰ s,
  { intro x_in_s,
    have : dist x x > 0, from gt_of_gt_of_ge (H x_in_s) (zero_le Î´).ge,
    exact (ne_of_gt this) (dist_self x)},
  have s_sub_t : s âŠ† t , from subset_insert x s,
  have s_ne_t : s â‰  t , from ne_insert_of_not_mem s x_notin_s,
  have t_sub_S : t âŠ† S, from insert_subset.mpr âŸ¨xS, s_sub_SâŸ©,
  have : coarsely_separated_with Î´ t,
  { rintros z (rfl | zs) y (rfl | ys),
    { exact Î» h, (h rfl).elim },
    { exact Î» hzy, H ys },
    { intro hzy,
      rw dist_comm,
      exact H zs },
    { exact s_sep zs ys }},
  exact s_ne_t (s_max t s_sub_t t_sub_S this),
end

/--
If `f g : Î¹ â†’ Î±` are `K`-close maps, the range of `g` is `K`-dense in the range of `f`
-/
lemma of_images_of_close_maps_with {K : â„} {f g : Î¹ â†’ Î±} (clw : close_maps_with K f g) :
  coarsely_dense_with_in K (range g) (range f) :=
begin
  rintros x x_in_rf,
  rcases x_in_rf with âŸ¨p,rflâŸ©,
  use [g p,by simp,clw p],
end

end coarsely_dense_with_in


namespace coarsely_separated_with

/--
A directed union of `Î´`-separated sets is a `Î´`-separated.
-/
lemma of_directed_union {Î´ : â„} {ğ’¸ : set $ set Î±}
  (allsep : âˆ€ s âˆˆ ğ’¸, coarsely_separated_with Î´ s)
  (dir : directed_on (âŠ†) ğ’¸) :
  coarsely_separated_with Î´ ğ’¸.sUnion :=
begin
  let ğ’ := ğ’¸.sUnion,
  rintros x x_in_ğ’,
  rcases set.mem_sUnion.mp x_in_ğ’ with âŸ¨t,t_in_ğ’¸,x_in_tâŸ©,
  rintros y y_in_ğ’,
  rcases set.mem_sUnion.mp y_in_ğ’ with âŸ¨r,r_in_ğ’¸,y_in_râŸ©,
  intro x_ne_y,
  rcases dir t t_in_ğ’¸ r r_in_ğ’¸ with âŸ¨s,s_in_ğ’¸,t_sub_s,r_sub_sâŸ©,
  have x_in_s : x âˆˆ s, from set.mem_of_subset_of_mem t_sub_s x_in_t,
  have y_in_s : y âˆˆ s, from set.mem_of_subset_of_mem r_sub_s y_in_r,
  let s_sep := set.mem_of_subset_of_mem allsep s_in_ğ’¸,
  exact s_sep x_in_s y_in_s x_ne_y,
end

/--
Given any `Î´` and subset `S` of `Î±`, there exists a maximal `Î´`-separated subset of `S`.
-/
theorem exists_max (Î´ : â„) (S : set Î±) :
  âˆƒ s : set Î±, s âŠ† S
             âˆ§ coarsely_separated_with Î´ s
             âˆ§ (âˆ€ t : set Î±, s âŠ† t â†’ t âŠ† S â†’  coarsely_separated_with Î´ t â†’ s = t) :=
begin
  let ğ’® : set (set Î±) :=  {s : set Î± | s âŠ† S âˆ§ coarsely_separated_with Î´ s},
  suffices : âˆƒ s âˆˆ ğ’®, âˆ€ t âˆˆ ğ’®, s âŠ† t â†’ t = s,
  { rcases this with âŸ¨s,âŸ¨s_sub_S,s_sepâŸ©,s_maxâŸ©, -- This whole block is just shuffling
    use [s,s_sub_S,s_sep],
    rintros t s_sub_t t_sub_S t_sep,
    have : t âˆˆ ğ’®, from âŸ¨t_sub_S,t_sepâŸ©,
    exact (s_max t â€¹t âˆˆ ğ’®â€º s_sub_t).symm,},
  apply zorn.zorn_subset,
  rintro ğ’¸ ğ’¸_sub_ğ’® ğ’¸_chain,
  have ğ’¸_sep : âˆ€ s âˆˆ ğ’¸, coarsely_separated_with Î´ s, from Î» s âˆˆ ğ’¸, (ğ’¸_sub_ğ’® H).right,
  let ğ’ := ğ’¸.sUnion,
  let ğ’_sep := of_directed_union ğ’¸_sep ğ’¸_chain.directed_on,
  use ğ’,
  split,
  { split,
    { apply set.sUnion_subset ,
      rintros s s_in_ğ’¸,
      exact (set.mem_of_subset_of_mem ğ’¸_sub_ğ’® s_in_ğ’¸).left,},
    {exact ğ’_sep,},},
  { rintros s s_in_ğ’¸,
    exact set.subset_sUnion_of_mem s_in_ğ’¸,},
end

end coarsely_separated_with

/--
Given any `Î´` and subset `S` of `Î±`, there exists a `Î´`-separated and `Î´`-dense subset of `S`.
-/
theorem exists_coarsely_separated_coarsely_dense_with_in (Î´ : â„â‰¥0) (S : set Î±) :
  âˆƒ s âŠ† S, coarsely_separated_with Î´ s
         âˆ§ coarsely_dense_with_in Î´ s S :=
begin
  rcases coarsely_separated_with.exists_max Î´ S with âŸ¨s, s_sub_S, s_sep, s_max_sepâŸ©,
  use [s,s_sub_S,s_sep],
  exact coarsely_dense_with_in.of_max_coarsely_separated_with_in âŸ¨s_sub_S, s_sep, s_max_sepâŸ©,
end


namespace close_maps_with

/--
Any map `f` is `0`-close to itself.
-/
lemma refl (f : Î¹ â†’ Î±) : close_maps_with 0 f f := Î» x, by simp

/--
Being `K`-close in symmetric.
-/
lemma symm {K : â„} {f g : Î¹ â†’ Î±} :
  close_maps_with K f g â†’  close_maps_with K g f :=
begin
  intros acw x,
  rw â†dist_comm,
  exact acw x,
end

/--
If `f` is `K`-close to `g`, which is `L`-close to `h`, then `f` is `(K+L)`-close to `h`.
-/
lemma trans {K L : â„} {f g h: Î¹ â†’ Î±} (c : close_maps_with K f g) (d : close_maps_with L g h) :
  close_maps_with (K + L) f h :=
Î» x, calc dist (f x) (h x)
        â‰¤ dist (f x) (g x) + dist (g x) (h x)   : dist_triangle _ _ _
    ... â‰¤ K                + L                  : add_le_add (c x) (d x)

/--
Precomposing `K`-close maps with any given map preserves `K`-closeness.
-/
lemma comp_left {K : â„} {Î¼ : Type*} {Ï† : Î¼ â†’ Î¹} {f g : Î¹ â†’ Î±} (clw : close_maps_with K f g) :
  close_maps_with K (f âˆ˜ Ï†) (g âˆ˜ Ï†) := Î» x, clw (Ï† x)

/--
If `f` is `K`-close to `g` and `K â‰¤ K'`, then `f` is `K'`-close to `g`.
-/
lemma weaken {K K' : â„} {f g: Î¹ â†’ Î±}  (leK : K â‰¤ K') (c : close_maps_with K f g)  :
  close_maps_with K' f g := Î» x, (c x).trans leK


/--
If `s` is `Îµ`-coarsely dense in `Î±`, there exists a retraction `ret: Î± â†’ s`
such that `coe âˆ˜ ret` is `Îµ`-close to the identities.
-/
lemma of_coarsely_dense_subset_with' {Îµ : â„â‰¥0} {s : set Î±} (cdw : coarsely_dense_with_in Îµ s univ) :
âˆƒ retract : Î± â†’ subtype s,
  close_maps_with Îµ (coe âˆ˜ retract) id âˆ§
  (retract âˆ˜ coe) = id :=
begin
    -- First we restate `cdw` in terms the axiom of choice likes:
  have cdw' : âˆ€ x : Î±, âˆƒ y : subtype s, (dist x â†‘y â‰¤ Îµ) âˆ§ (x âˆˆ s â†’ x = â†‘y), by
  { intro x,
    by_cases h : x âˆˆ s,
    { use [x, h],
      simp,},
    { rcases cdw (mem_univ x) with âŸ¨y,ys,ydâŸ©,
      use [âŸ¨y,ysâŸ©,yd],},},
  rcases classical.axiom_of_choice cdw' with âŸ¨ret, goodâŸ©,
  use ret,
  split,
  { intros x,
    dsimp,
    specialize good x,
    rw dist_comm,
    exact good.1,},
  { apply funext,
    rintros âŸ¨x,x_in_sâŸ©,
    specialize good x,
    ext,
    exact (good.2 x_in_s).symm,},
end

/--
If `s` is `Îµ`-coarsely dense in `Î±`, there exists a map `ret: Î± â†’ s`
such that the two composites of `ret` with `coe: s â†’ Î±` are `Îµ`-close to the identities.
-/
lemma of_coarsely_dense_subset_with {Îµ : â„â‰¥0} {s : set Î±} (cdw : coarsely_dense_with_in Îµ s univ) :
âˆƒ retract : Î± â†’ subtype s,
  close_maps_with Îµ (coe âˆ˜ retract) id âˆ§
  close_maps_with Îµ (retract âˆ˜ coe) id :=
begin
  rcases of_coarsely_dense_subset_with' cdw with âŸ¨ret,left,rightâŸ©,
  exact âŸ¨ret,left,right.symm â–¸ ((close_maps_with.refl id).weaken (zero_le Îµ))âŸ©
end

end close_maps_with
